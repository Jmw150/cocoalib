Package $binrepr -- Binomial Representation

export BinExp;
export EvalBinExp;
  
define About()
  println "    Author: A.M.Bigatti";
  println "    Date: 10 April 2001  (corrected use of \"End;\" 4 July 2002)";
  println "    BugFix: new tag handling";
enddefine;


Define Man()
  PrintLn "SYNTAX";
  PrintLn "    $binrepr.PolyBinRepr_xi(UnivariatePoly): TAGGED(\"BinRepr\")";
  PrintLn "    $binrepr.PolyBinRepr_xii(UnivariatePoly): TAGGED(\"BinRepr\")";
  PrintLn;
  PrintLn "DESCRIPTION";
  PrintLn;
  PrintLn;
  PrintLn ">EXAMPLE<";
  PrintLn;
  PrintLn "    $binrepr.Initialize();";
  PrintLn "    $binrepr.PolyBinRepr_xi(x^2);";
  PrintLn "    $binrepr.PolyBinRepr_xii(x^2);";
  PrintLn "    MEMORY.PKG.BinRepr.Constant := 100;";
  PrintLn "    $binrepr.PolyBinRepr_xi(x^2);";
  PrintLn "    $binrepr.PolyBinRepr_xii(x^2);";
EndDefine;

--------------------------------------------------------------
-- TAG "Bin";     --  [Bin]omial Coefficient
--     "BinRepr"; --  [Bin]omial [Repr]esentation

-- TYPE BinRepr := LIST(BRTerm);    --> TAGGED "BinRepr"
--      BRTerm  := Record[BIN, COEFF];
--      Bin     := [POLY, INT];     --> TAGGED "Bin"
--      COEFF   := {INT, RAT};

-- Package variables
Constant := 0;


define PolyBinRepr_xi(P)
  Idt := UnivariateIndetIndex(P);
  if Idt=0 then error("PolyBinRepr_xi: Expected univariate polynomial"); endif;
  idt := indet(RingOf(P), Idt);
  BE := [];  C := $.Constant;
  while P <> 0 do
    I := deg(P);
    append(ref BE, record[COEFF := LC(P)*factorial(I), BIN := [Idt+C,I]] );
    P := P-(AsRAT(LC(P))*factorial(I)*binomial(Idt+C,I));
  endwhile;
  return $.Tagged(BE,"BinRepr");
enddefine; -- PolyBinRepr_xi


Define PolyBinRepr_xii(P)
  Idt := UnivariateIndetIndex(P);
  If Idt=0 Then error("PolyBinRepr_xii: Expected univariate polynomial"); EndIf;
  Idt := indet(RingOf(P), Idt);
  BE := [];  C := $.Constant;
  While P <> 0 Do
    I := deg(P);
    append(ref BE, record[COEFF := AsRAT(LC(P))*factorial(I), BIN := [Idt+C+I,I]] );
    P := P-(AsRAT(LC(P))*factorial(I)*binomial(Idt+C+I,I));
  EndWhile;
  Return $.Tagged(BE,"BinRepr");
EndDefine;


Define BinExp(...)
  TopLevel ERR;
  If shape(ARGV)=[INT, INT] Then Return $.Aux_BinExp(ARGV[1], ARGV[2]);
  Elif shape(ARGV)=[INT, INT, INT, INT] Then
    Return EvalBinExp($.Aux_BinExp(ARGV[1],ARGV[2]), ARGV[3], ARGV[4]);
  Else error(ERR.BAD_PARAMS,"(BinExp arguments must be 2 or 4 INT)");
  EndIf;
EndDefine;


Define EvalBinExp(BE,Up,Down)
  return sum([$hp.TruncBin(B[1]+Up,B[2]+Down) | B In untagged(BE)]);
EndDefine; -- EvalBinExp


Define Aux_BinExp(H,N)
  TopLevel ERR;
  If H<0 Or N<=0 Then error(ERR.BAD_PARAMS, "(BinExp(H,N), H>=0, N>0)"); EndIf;
  BE:=[];  HH:=H;  NN:=N;
  While HH <> 0 Do
    I := NN+1;  BIN := I;  --> BIN:=binomial(NN+1,NN)
    While BIN <= HH Do  I:=I+1; BIN:=(BIN/(I-NN))*I;  EndWhile;
    append(ref BE,[I-1,NN]);
    HH := HH - (BIN/I)*(I-NN); NN:=NN-1;
  EndWhile;
  Return $.Tagged(BE,"BinExp");
EndDefine;


--------[   pretty printing   ]--------

Define Tagged(X, T)
  Return tagged(X, T);  // CoCoA-5 builtin "tagged" for packages
EndDefine; -- Tagged

Define Print_Bin(D, B)
  Print "binomial(", B[1], ",",B[2], ")" On D;
EndDefine;


Define Print_BinExp(D, BE)
  If untagged(BE) = [] Then Print 0 On D;
  Else
    Print $.Tagged(BE[1],"Bin") On D;
    Foreach BIN In tail(BE) Do Print " + ", $.Tagged(BIN,"Bin") On D; EndForeach;
  EndIf;
EndDefine;


Define Print_BinRepr(D, BR)
  $sppoly.PrintFirstCoeff(BR[1].COEFF, False, D);
  Print " ", $.Tagged(BR[1].BIN,"Bin") On D;
  foreach BT in tail(BR) do
    $sppoly.PrintCoeff(BT.COEFF,False,D);
    Print " ", $.Tagged(BT.BIN,"Bin") On D;
  EndForeach;
EndDefine;

----------------------------------------------------------------------
PrintTagged := Record[
		      Bin := $.Print_Bin,
		      BinExp := $.Print_BinExp,
		      BinRepr := $.Print_BinRepr
		      ];

EndPackage; -- Package $binrepr

